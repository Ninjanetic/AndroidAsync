apply plugin: 'com.android.library'

android {
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'

            jniLibs.srcDirs = ['libs/']

            java.srcDirs=['src/'
//                          , '../conscrypt/'
//                          , '../compat/'
            ]
        }
        androidTest.java.srcDirs=['test/src/']
        androidTest.res.srcDirs=['test/res/']
        androidTest.assets.srcDirs=['test/assets/']
    }

    lintOptions {
        abortOnError false
    }

    defaultConfig {
        targetSdkVersion 22
        minSdkVersion 9
    }

    compileSdkVersion 21
    buildToolsVersion '21.1.2'
}

apply plugin: 'maven'
apply plugin: 'signing'

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

group = "com.digium.respoke"
version = "2.1.7"   // Should match the version in AndroidManifest.xml

// upload to maven task
if (System.getenv().I_AM_KOUSH == 'true') {
  apply from: 'https://raw.githubusercontent.com/koush/mvn-repo/master/maven.gradle'
}

uploadArchives {
    repositories {
        apply plugin: 'maven'
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'AndroidAsync'
                artifactId 'AndroidAsync'
                packaging 'aar'
                description 'Asynchronous socket, http (client+server), websocket, and socket.io library for android. Based on nio, not threads.'
                url 'https://github.com/respoke/AndroidAsync'

                // The scm section may look like a copy/paste error, but it's not.
                scm {
                    connection 'scm:git@github.com:respoke/respoke-sdk-android.git'
                    developerConnection 'scm:git@github.com:respoke/respoke-sdk-android.git'
                    url 'https://github.com/respoke/respoke-sdk-android.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'jpadams'
                        name 'Respoke.io'
                        email 'info@respoke.io'
                    }
                }
            }
        }
    }
}
